'$INCLUDE:'..\ddlib\String.bm'

$if path = undefined then
$let path = defined

' ----------------------------------------------------------------------------
' Adds a slash (if needed) to a path expression.
'___
' @param {string} path Specifies the path to which to add the slash (checks the operating system)
function path.AddSlash$(path as string)
	dim retvalue as string : retvalue = rtrim$(path)
	dim slash as string * 1 : slash = ""
	if instr(_os$,"[WINDOWS]") > 0 then
		slash = "\"
	else
		slash = "/"
	end if
	if right$(retvalue, 1) <> slash then
		retvalue = retvalue + slash
	end if
	path.AddSlash$ = retvalue
end function
' ----------------------------------------------------------------------------
' Changes the slashes based on the operating system
' - LINUS & OSX = "/"
' - WINDOWS = "\\"
'___
' @param {string} path Specifies the path to which to fix the slashs (checks the operating system)
function path.FixSlashes$(path as string)
	dim retvalue as string : retvalue = path
	if instr(_os$,"[WINDOWS]") > 0 then
		retvalue = string.ReplaceAll$(retvalue, "/", "\")
	else
		retvalue = string.ReplaceAll$(retvalue, "\", "/")
	end if
	path.FixSlashes$ = retvalue
end function

$end if